/-
Copyright (c) 2024 Joris Roos. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joris Roos
-/
import BooleanFun.BooleanValued

-- set_option profiler true

/-!
# Arrow's theorem

This file proves a version of Arrow's theorem [arrow1950] for 3-candidate elections,
see `dictator_of_condorcet_and_unanimous`.
We follow O'Donnell [odonnell2014], Sec. 2.5,
which follows Kalai's approach [kalai2002] via Fourier analysis of Boolean valued functions.

## Implementation notes

The proof in [odonnell2014] makes key use of notions from probability such as
joint probability distributions on the discrete cube. We prefer to avoid this in this formalization,
so we unpack most of the probability language in the proof.
This is mainly facilitated by introducing an auxiliary linear operator, see `_Tnae3`.

## References
* [K. Arrow, *A difficulty in the concept of social welfare*][arrow1950]
* [G. Kalai, *A Fourier-theoretic perspective on the Condorcet paradox and Arrow's
theorem*][kalai2002]
* [R. O'Donnell, *Analysis of Boolean functions*][odonnell2014]

## Extensions?
* Allow different voting rules for each pairwise election
* Guilbaud's formula (needs CLT)
* Stability results -- one of the great benefits of the Fourier approach

-/

noncomputable section

namespace BooleanFun.BV

open Classical Mathlib Finset Pi

variable {Œ±:Type*} {Œπ:Type*}
variable {n:‚Ñï}

variable {f:BooleanFunc n} [hbv: BooleanValued f]

/-- Encodes votes of `n` voters in a 2-candidate election. -/
abbrev Votes n := Fin n ‚Üí Fin 2

/-- `¬±1`-valued majority function -/
def majority: BooleanFunc n := Œª x => if ‚àë j,  (x j).val > n/2 then 1 else -1

/-- The majority function is Boolean valued. -/
instance : BooleanValued (@majority n) where one_or_neg_one := by {
  intro x
  rw [majority.eq_def]
  split_ifs
  ¬∑ left; rfl
  ¬∑ right; rfl
}

/-- A dictator is a Walsh character of a singleton set. -/
abbrev dictator {n:‚Ñï} (i:Fin n): BooleanFunc n := œá {i}

/-- Not-all-equal predicate on three values. -/
abbrev NAE3 (x y z: Œ±) : Prop := ¬¨ (x = y ‚àß y = z)

/-- Voter preferences in an election among 3 candidates A,B,C are represented by three
vote ensembles `x y z` representing votes in the three 2-candidate elections among the candidates:
`x` contains the votes for the election A vs. B, `y` for B vs. C and `z` for C vs. A.
Three vote ensembles `x y z` are consistent if they encode a ranking of the
three candidates A,B,C for each voter. This is expressed by the not-all-equal predicate.
See [odonnell2014], Sec. 2.5. -/
def VoteConsistent (x y z: Votes n) : Prop :=
    ‚àÄ i, NAE3 (x i) (y i) (z i)

/-- Commute arguments of `VoteConsistent` predicate -/
lemma VoteConsistent.comm_right {x y z: Votes n}:
    VoteConsistent x y z = VoteConsistent x z y := by
  unfold VoteConsistent NAE3; ext
  constructor <;> { intro h i; specialize h i; push_neg; intro h'; rw [h'] at h; tauto }

/-- Commute arguments of `VoteConsistent` predicate -/
lemma VoteConsistent.comm_rcyc {x y z: Votes n}:
    VoteConsistent x y z = VoteConsistent y z x := by
  unfold VoteConsistent NAE3; ext
  constructor <;> { intro h i; specialize h i; push_neg; intro h'; rw [h'] at h; tauto }

/-- A voting rule is Condorcet, if in every 3-candidate election conducted
  using it there is a Condercet winner. -/
def IsCondorcet (f:BooleanFunc n): Prop :=
    ‚àÄ x y z, VoteConsistent x y z ‚Üí NAE3 (f x) (f y) (f z)

/-- A voting rule admits a dictator if it is equal to `dictator i` for some `i`. -/
def HasDictator (f:BooleanFunc n): Prop :=
    ‚àÉ i, f = dictator i

/-- A voting rule is unanimous if it selects candidate `i` if everyone votes for `i`.  -/
def IsUnanimous (f:BooleanFunc n): Prop := f 0 = 1 ‚àß f 1 = -1

/-- The (unique) voting rule for zero voters is not unanimous. -/
lemma zero_not_unanimous (f:BooleanFunc n): n=0 ‚Üí ¬¨IsUnanimous f := by
  unfold IsUnanimous
  push_neg
  intro hn h
  have : (1:Fin n‚ÜíFin 2) = 0 := by
    rw [hn]; trivial
  rw [this, h]
  norm_num

/-- Explicit Walsh-Fourier expansion of the not-all-equal predicate on a 3-tuple composed with a Boolean valued function.
A crucial step in the proof of Arrow's theorem. -/
lemma oneOn_NAE3_eq {x y z:Fin n‚ÜíFin 2}:
    oneOn (NAE3 (f x) (f y) (f z)) = 3/4 - (1/4)*(f x)*(f y) - (1/4)*(f y)*(f z) - (1/4)*(f x)*(f z) := by
  unfold oneOn NAE3
  obtain ‚ü®h0|h0,h1|h1,h2|h2‚ü© := And.intro (hbv.one_or_neg_one x)
    (And.intro (hbv.one_or_neg_one y) (hbv.one_or_neg_one z))
    <;> { rw [h0, h1, h2]; norm_num }

/-- The probability of a Condorcet winner equals the proportion out of
    the `6‚Åø` possible voter preferences `(x,y,z)` so that `(f(x), f(y), f(z))`
    is a consistent preference tuple. The impartial culture assumption is encoded
    by giving each tuple `(x,y,z)` the same weight. -/
def probabilityCondorcetWinner (f:BooleanFunc n): ‚Ñù :=
    (1/6)^n*‚àë x, ‚àë y, ‚àë z, oneOn (NAE3 (f x) (f y) (f z))
      * oneOn (VoteConsistent x y z)

/-- Auxiliary lemma for the proof of `probabilityCondorcetWinner_eq_one` -/
lemma _triple_sum_oneOn_consistent_eq:
    ‚àë x:Fin n‚ÜíFin 2, ‚àë y:Fin n‚ÜíFin 2, ‚àë z:Fin n‚ÜíFin 2, oneOn (VoteConsistent x y z) = 6^n := by
  unfold VoteConsistent
  simp_rw [‚ÜêoneOn_prod]
  conv => enter [1, 2, x, 2, y]
          rw [sum_tuple_prod_distrib (f:=Œª i‚Ü¶Œªc‚Ü¶oneOn (NAE3 (x i) (y i) c))]
  -- = ‚àë x, ‚àè i, ‚àë b, ‚àë c, oneOn (NAE3 (x i) b c)
  conv => enter [1, 2, x]
          rw [sum_tuple_prod_distrib (f:=Œª i‚Ü¶Œªb‚Ü¶‚àë c, oneOn (NAE3 (x i) b c))]
  -- = ‚àè i, ‚àë a, ‚àë b, ‚àë c, oneOn (NAE3 a b c)
  rw [sum_tuple_prod_distrib (f:=Œª _‚Ü¶Œªa‚Ü¶‚àë b, ‚àë c, oneOn (NAE3 a b c))]
  -- = 6^n
  norm_num

omit hbv in
/-- If a voting rule is Condorcet, then the probability of a Condorcet winner equals 1. -/
lemma probabilityCondorcetWinner_eq_one (hc: IsCondorcet f):
    probabilityCondorcetWinner f = 1 := by
  -- 6^n*LHS = ‚àë x, ‚àë y, ‚àë z, ‚àè i, oneOn (NAE3 (x i) (y i) (z i))
  have : ‚àÄ x y z:Fin n‚ÜíFin 2, NAE3 (f x) (f y) (f z) ‚àß VoteConsistent x y z ‚Üî
      VoteConsistent x y z := by
    intro x y z
    specialize hc x y z
    tauto
  unfold probabilityCondorcetWinner
  simp_rw [‚ÜêoneOn_and, this, _triple_sum_oneOn_consistent_eq]
  simp

/-- Auxiliary linear operator -/
private abbrev _Tnae3: BooleanFunc n ‚Üí‚Çó[‚Ñù] BooleanFunc n := {
  toFun := Œª f =>
    Œª x => (1/3)^n*‚àë y, f y*‚àë z:Fin n‚ÜíFin 2, oneOn (VoteConsistent x y z)
  map_add' := by
    intro f g
    funext x
    simp only [one_div, inv_pow, add_apply, sum_boole]
    conv => enter [1,2,2,y]; rw [add_mul]
    rw [sum_add_distrib, mul_add]
  map_smul' := by
    intro c f
    funext x
    simp only [smul_apply, smul_eq_mul, RingHom.id_apply, add_apply, id_eq, eq_mpr_eq_cast,
      AddHom.toFun_eq_coe, AddHom.coe_mk]
    conv => enter [1,2,2,y]; rw [mul_assoc]
    rw [‚Üêmul_sum, ‚Üêmul_assoc, mul_comm _ c, mul_assoc]
}

/-- Notation for the auxiliary linear operator `_Tnae3`-/
local notation "T" => _Tnae3

/-- One of two crucial steps in the proof of Arrow's theorem:
 the auxiliary linear operator can be expressed in terms of `noise_operator`. -/
lemma _eq_noise_operator: T = @noise_operator n (-1/3) := by
  apply Basis.ext (b:=walsh_basis)
  intro S
  unfold walsh_basis
  unfold noise_operator
  simp only [coe_basisOfOrthonormalOfCardEqFinrank, LinearMap.coe_mk,
    AddHom.coe_mk]
  rw [multiplier_walsh]
  funext x
  rw [smul_apply, smul_eq_mul]
  have : ‚àÄ y:Fin n‚ÜíFin 2, ‚àë z:Fin n‚ÜíFin 2, oneOn (VoteConsistent x y z)
      =  ‚àè i, (1+oneOn (x i‚â†y i)) := by
    -- ‚àë z:Fin n‚ÜíFin 2, oneOn (VoterPreferencesConsistent x y z)
    -- = ‚àë z:Fin n‚ÜíFin 2, ‚àè i, oneOn (NAE3 (x i) (y i) (z i))
    intro y
    unfold VoteConsistent
    simp_rw [‚ÜêoneOn_prod]
    -- = ‚àè i, ‚àë v:Fin 2, oneOn (NAE3 (x i) (y i) v)
    rw [sum_tuple_prod_distrib (f:=Œªi=>Œªv=>oneOn (NAE3 (x i) (y i) v))]
    suffices ‚àÄ i, ‚àë v : Fin 2, oneOn (NAE3 (x i) (y i) v) = (1+oneOn (x i‚â†y i)) by simp_rw [this]
    -- for each i, two cases: x i = y i or x i ‚â† y i
    intro i
    by_cases h:x i = y i
    ¬∑ simp [h]
      induction y i using Fin.cases with
      | zero => simp
      | succ k => simp [Fin.fin_one_eq_zero]
    ¬∑ simp [h]; norm_num
  -- ‚àë y : Fin n ‚Üí Fin 2, ‚àè i‚ààS, (-1)^(y i)*‚àè i, (1+oneOn (x i‚â†y i))
  conv => enter [1,2,2,y]; rw [this, walshCharacter.eq_def]
          enter [1,1]; rw [‚Üêfilter_univ_mem (s:=S)]
  -- = ‚àë y : Fin n ‚Üí Fin 2, ‚àè i, (-1)^(oneOn (i‚àà S)*(y i))*(1+oneOn (x i‚â†y i))
  conv => enter [1,2,2,y]; rw [prod_filter, ‚Üêprod_mul_distrib]
  -- = ‚àè i, ‚àë v:Fin 2,  (-1)^(oneOn (i‚àà S)*v)*(1+oneOn (x i‚â†v))
  rw [sum_tuple_prod_distrib (f:=Œª i=>Œª v=>(if i‚ààS then (-1)^v.val else 1) * (1 + oneOn (x i ‚â† v)))]
  -- = ‚àè i, (-1)^(oneOn (i‚àà S)*(x i)) + (-1)^(oneOn (i‚àà S)*(1-x i))*2
  have haux (w:Fin 2) (g:Fin 2‚Üí‚Ñù): ‚àë v:Fin 2, g v = g w + g (1-w) := by
    induction w using Fin.cases with -- TODO: tactic macro?
    | zero => simp
    | succ k => simp [Fin.fin_one_eq_zero k]; exact add_comm _ _
  conv => enter [1,2,2,i]; rw [haux (x i), oneOn_false (by simp)]
          rw [oneOn_true (by decreasing_trivial), add_zero, mul_one]
  -- = ‚àè i‚ààS, ((-1)^(x i) + 2*(-1)^(1-x i)) * ‚àè i‚àâS, ..
  rw [‚Üêprod_filter_mul_prod_filter_not (p:=Œª i=>i‚ààS), prod_filter, prod_filter]
  -- first product equals (-1)^S.card*‚àè i‚ààS, (-1)^(x i) = (-1)^S.card*œá S x
  have haux2 (v:Fin 2): ((-1):‚Ñù)^v.val + (-1)^(1-v).val*(1+1) = (-1)*(-1)^v.val := by
    induction v using Fin.cases with
    | zero => simp
    | succ k => simp [Fin.fin_one_eq_zero k]
  conv => enter [1,2,1,2,i]; rw [ite_mul, ite_add_ite, ite_ite_same, haux2]
  rw [‚Üêprod_filter, filter_univ_mem, prod_mul_distrib, prod_const, ‚Üêwalsh_def]
  -- second product equals ‚àè i‚àâS, 3 = 3^(n-S.card)
  conv => enter [1,2,2,2,i]; rw [ite_mul, ite_add_ite, ite_ite_not']; arg 2; norm_num
  rw [‚Üêprod_filter, prod_const, filter_univ_not_mem, card_compl, Fintype.card_fin]
  rw [npow_sub‚ÇÄ (by simp) (card_finset_fin_le _)]
  field_simp -- todo: speedup

/-- The probability of having a Condorcet winner can be expressed in terms of the noise operator.
See [odonnell2014], Theorem 2.56. -/
theorem probabilityCondorcetWinner_eq:
    probabilityCondorcetWinner f = 3/4*(1- noise_stability (-1/3) f) := by
  simp_rw [probabilityCondorcetWinner.eq_def, oneOn_NAE3_eq,sub_mul, sum_sub_distrib]
  -- the first sum is just equal to 3/4
  conv => enter [1,2,1,1,1]; tactic => simp_rw [‚Üêmul_sum]
  rw [_triple_sum_oneOn_consistent_eq]
  -- rewrite fourth sum to equal second
  conv => enter [1,2,2,2,x]; rw [sum_comm]; enter [2,y,2,z];
          rw [VoteConsistent.comm_right]
  -- rewrite third sum to equal second
  conv => enter [1,2,1,2]; rw [sum_comm]; enter [2,x]; rw [sum_comm];
          enter [2,y,2,z]; rw [VoteConsistent.comm_rcyc]
  -- collect the three sums
  rw [sub_sub, sub_sub, ‚Üêtwo_mul]
  nth_rewrite 1 [‚Üêone_mul (‚àëx,_)]
  rw [‚Üêadd_mul]
  -- take out factor (1/4)
  conv => enter [1,2,2,2,2,x,2,y,2,z]; rw [mul_assoc, mul_assoc]
  conv => enter [1,2,2,2]; tactic => simp_rw [‚Üêmul_sum]
  -- rewrite in terms of T and apply main lemma `_eq_noise_operator`
  have : ‚àë x, f x*‚àë y, f y*‚àë z, oneOn (VoteConsistent x y z)
      = 6^n*noise_stability (-1/3) f := by
    calc
      _ = 3^n*(1/3)^n*‚àë x, f x*‚àë y, f y*‚àë z, oneOn (VoteConsistent x y z) := by simp only [mul_ite,
          mul_one, mul_zero, one_div, inv_pow, isUnit_iff_ne_zero, ne_eq, pow_eq_zero_iff',
          OfNat.ofNat_ne_zero, false_and, not_false_eq_true, IsUnit.mul_inv_cancel, one_mul]
      _ = 3^n*‚àë x, f x*T f x := by rw [mul_assoc, mul_sum]; conv => {enter [1,2,2,x]; rw [‚Üêmul_assoc, mul_comm _ (f x), mul_assoc]}; rfl
      _ = 3^n*2^n*(1/2)^n*‚àë x, f x*T f x := by simp only [LinearMap.coe_mk, one_div, inv_pow,
          sum_boole, AddHom.coe_mk, isUnit_iff_ne_zero, ne_eq, pow_eq_zero_iff', OfNat.ofNat_ne_zero,
          false_and, not_false_eq_true, IsUnit.mul_inv_cancel_right]
      _ = 6^n*(1/2)^n*‚àë x, f x*T f x := by congr; rw [‚Üêmul_pow]; congr; norm_num
      _ = 6^n*‚ü®f, T f‚ü© := by rw [mul_assoc]; rfl
      _ = 6^n*‚ü®f, noise_operator (-1/3) f‚ü© := by rw [_eq_noise_operator]
  rw [this]
  field_simp -- todo: speedup
  ring

/-- Arrow's theorem as formulated in [odonnell2014], Sec. 2.5: Every unanimous voting rule that always admits a Condorcet winner is a dictatorship. -/
-- One can generalize this to usee three different voting rules `f,g,h`.
theorem dictator_of_condorcet_and_unanimous (h:IsUnanimous f):
    IsCondorcet f ‚Üí HasDictator f := by
  wlog hn: 0 < n
  ¬∑ have := zero_not_unanimous f (Nat.eq_zero_of_not_pos hn)
    contradiction
  intro hc
  have := probabilityCondorcetWinner_eq_one hc
  let œÅ:‚Ñù := -1/3
  have : noise_stability œÅ f = œÅ := by
    rw [probabilityCondorcetWinner_eq] at this
    calc
      _ = 1-4/3*(3/4*((1:‚Ñù)-(noise_stability œÅ f))) := by ring
      _ = _   := by rw [this]; ring
  have hsumzero: ‚àë S, (œÅ^S.card - œÅ)*|ùìï f S|^2 = 0 := by
    simp_rw [sub_mul, sum_sub_distrib]
    rw [‚Üênoise_stability_eq_sum_fourier, this]
    nth_rewrite 1 [‚Üêmul_one œÅ]
    rw [‚Üêfourier_eq_one (f:=f), mul_sum, sub_self]
  have hmz: ‚àÄ S‚ààuniv, (œÅ^S.card - œÅ)*|ùìï f S|^2 = 0 := by {
    have: ‚àÄ S ‚àà univ, 0 ‚â§ (œÅ ^ S.card - œÅ) * |ùìï f S| ^ 2 := by
      intro S _
      apply mul_nonneg
      ¬∑ by_cases hk: Even S.card
        ¬∑ obtain ‚ü®k,hk‚ü© := hk
          rw [hk, ‚Üêtwo_mul]
          apply add_nonneg
          ¬∑ calc
              0 ‚â§ (œÅ^2)^k := pow_nonneg (pow_two_nonneg _) _
              _ = œÅ^(2*k) := by rw [pow_mul]
          ¬∑ norm_num
        ¬∑ obtain ‚ü®k,hk‚ü© := Nat.not_even_iff_odd.mp hk
          rw [hk]
          calc
            0 ‚â§ (-œÅ)*(1-(œÅ^2)^k)    :=
              by apply mul_nonneg; norm_num; apply sub_nonneg.mpr;
                 exact pow_le_one‚ÇÄ (pow_two_nonneg œÅ) (by norm_num)
            _ = (-œÅ)*(1-œÅ^(2*k))    := by rw [pow_mul]
            _ = œÅ^(2*k+1)-œÅ         := by ring
      ¬∑ exact sq_nonneg _
    apply eq_zero_of_nonneg_and_sum_eq_zero this hsumzero
  }
  have hnez: ‚àÄ k:‚Ñï, k‚â†1 ‚Üí œÅ^k - œÅ ‚â† 0 := by
    intro k
    apply not_imp_not.mpr
    intro h
    have h := eq_add_of_sub_eq h
    rw [zero_add] at h
    obtain h|h|h|h := pow_eq_self_imp h
      <;> first | assumption | norm_num at h
  have: ‚àÄ S, S.card ‚â† 1 ‚Üí ùìï f S = 0 := by
    intro S hS
    have := hmz S (by simp)
    have := eq_zero_of_ne_zero_of_mul_left_eq_zero (hnez S.card hS) this
    simp at this
    assumption
  have := fourier_eq_zero_iff_fourier_weight_eq.mp this
  rw [norm_sq_eq_one] at this
  obtain ‚ü®i,‚ü®c,hfeq‚ü©‚ü© := eq_character_of_fourier_weight_one_eq_one hn this
  use i
  have := funext_iff.mp hfeq 0
  rw [h.1] at this
  simp at this
  rw [hfeq, ‚Üêthis]
  simp

end BooleanFun.BV

--#lint
