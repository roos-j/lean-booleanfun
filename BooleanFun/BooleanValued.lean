/-
Copyright (c) 2024 Joris Roos. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joris Roos
-/
import BooleanFun.Basic

--set_option profiler true

/-!
# Boolean valued functions

This file introduces a typeclass `BooleanValued` for Boolean functions only taking values `Â±1`
and proves some basic properties specific to Boolean-valued functions.

## Main results

* `eq_character_of_fourier_weight_one_eq_one`, used in the proof of Arrow's theorem
* `almost_character` -- a theorem on BLR linearity testing
-/

noncomputable section

namespace BooleanFun

open BooleanFun Finset Function Fin

variable {n:â„•} {f g:BooleanFunc n} {x:Fin nâ†’Fin 2}

/-- `BooleanValued f` bundles a proof that `f` takes values `Â±1`. -/
class BooleanValued (f: BooleanFunc n): Prop where
  one_or_neg_one: âˆ€ x, f x = 1 âˆ¨ f x = -1

namespace BV

variable [hbv: BooleanValued f] [hbvg: BooleanValued g]

lemma eq_one_or_eq_neg_one: âˆ€ x, f x = 1 âˆ¨ f x = -1 := hbv.one_or_neg_one

lemma norm_sq_eq_one:
    â€–fâ€–^2 = 1 := by
  unfold norm instNormBooleanFunc InnerProductSpace.Core.toNorm
  dsimp
  rw [inner_eq_inner_product]
  unfold inner_product
  conv in f * f =>
    ext x
    dsimp
    tactic =>
      have : f x * f x = 1 := by
        cases hbv.one_or_neg_one x with | _ => simp [*]
    rw [this]
  have : (Î»_:Fin nâ†’Fin 2â†¦(1:â„)) = (1:BooleanFunc n) := by ext; simp
  simp [this]

lemma fourier_eq_one:
    âˆ‘ S, |ğ“• f S|^2 = 1 := by
  rw [â†walsh_plancherel]
  exact norm_sq_eq_one

lemma eq_neg_one_of_ne_one (h':f xâ‰ 1) : f x = -1 :=
  or_iff_not_imp_left.mp (hbv.one_or_neg_one x) h'

lemma eq_one_of_ne_neg_one (h':f xâ‰ -1) : f x = 1 :=
  or_iff_not_imp_right.mp (hbv.one_or_neg_one x) h'

instance neg_boolean_valued [hbv: BooleanValued f]: BooleanValued (-f) where
  one_or_neg_one := by intro x; rw [Pi.neg_apply, neg_inj, neg_eq_iff_eq_neg, or_comm]; exact hbv.one_or_neg_one _

/-- Walsh characters are Boolean valued -/
instance (S:Finset (Fin n)): BooleanValued (Ï‡ S) where
    one_or_neg_one := by simp_rw [walsh_eq_neg_one_pow_sum, neg_one_pow_eq_or _, implies_true]

section DegreeOne

/-- A Boolean valued function that is a linear combination of degree one characters must be constant
times a degree one character.
Most involved step towards `eq_character_of_fourier_weight_one_eq_one`. -/
-- Unfortunately a bit lengthy
lemma eq_character_of_eq_sum_degree_one (hn:n>0) (hf:âˆ€ x, f x = âˆ‘ i, ğ“• f {i}*(-1)^(x i).val):
    âˆƒ Sâˆˆ{S|S.card = 1}, âˆƒ c:â„, f = câ€¢Ï‡ S := by
  wlog hf1: f 0 = 1 with h1
  { -- replace f by -f
    -- have h': IsBooleanValued (-f) := neg_is_boolean_valued (h:=h)
    have hf' : âˆ€ x, (-f) x = âˆ‘ i, ğ“• (-f) {i}*(-1)^(x i).val := by intro x; simp; exact hf x
    have : (-f) 0 = 1 := by
      rw [Pi.neg_apply, neg_eq_iff_eq_neg]; exact eq_neg_one_of_ne_one hf1
    specialize h1 hn hf' this
    obtain âŸ¨S, hS0, hS1âŸ© := h1
    obtain âŸ¨c, hcâŸ© := hS1
    use S
    constructor
    Â· assumption
    Â· use -c
      rw [neg_smul, â†hc, neg_neg]
  }

  cases n with
  | zero => contradiction
  | succ n =>
    induction n with
    | zero => -- only one coordinate
      use {0}
      simp
      use 1
      simp
      ext x
      rw [hf] at hf1
      simp at hf1
      rw [hf]
      simpa
    | succ n hi => -- will reduce one coordinate
      have hf0eq : f 0 = âˆ‘ i, ğ“• f {i} := by
        rw [hf]
        apply sum_congr (by rfl)
        intro i _
        simp
      have hf0feq : âˆ€ iâ‚€, f (flipAt iâ‚€ 0) = âˆ‘ iâˆˆuniv.erase iâ‚€, ğ“• f {i} + -ğ“• f {iâ‚€}:= by
        intro iâ‚€
        rw [hf]
        rw [â†sum_erase_add (a:=iâ‚€)]
        congr 1
        Â· apply sum_congr (by rfl)
          intro i hi
          rw [flipAt_unflipped (h:=ne_of_mem_erase hi)]
          rw [Pi.zero_apply, Fin.val_zero, pow_zero, mul_one]
        Â· rw [flipAt_flipped]
          rw [Pi.zero_apply, sub_zero, Fin.val_one, pow_one, mul_neg, mul_one]
        Â· exact mem_univ iâ‚€
      -- key step:
      have: âˆƒ iâ‚€, f (flipAt iâ‚€ 0) = 1 := by
        by_contra hc -- suppose not
        have hfm1: âˆ€i, f (flipAt i 0) = -1 := by
          intro i; exact eq_neg_one_of_ne_one ((not_exists.mp hc) i)
        have : âˆ€ i, ğ“• f {i} = 1 := by
          intro i; symm
          calc
            1 = (1 - (-1))/2              := by norm_num
            _ = (f 0 - f (flipAt i 0))/2 := by rw [hf1, hfm1]
            _ = (âˆ‘ i', ğ“• f {i'} - âˆ‘ i'âˆˆuniv.erase i, ğ“• f {i'} + ğ“• f {i})/2 := by rw [hf0eq, hf0feq]; ring
            _ = _                         := by rw [â†sum_erase_add (a:=i)]; ring; exact mem_univ i
        have : f 0 = n+2 := by
          rw [hf0eq]
          conv => enter [1,2,i]; rw [this]
          simp; ring
        have : (1:â„) â‰¥ 2 := by
          calc 1 = f 0   := by symm; exact hf1
               _ = n + 2 := by exact this
               _ â‰¥ 0 + 2 := by gcongr; exact Nat.cast_nonneg n
               _ = 2     := by simp
        have : Â¬(1:â„)â‰¥2  := by simp
        exact this (by assumption)
      obtain âŸ¨iâ‚€,hiâ‚€âŸ© := this -- this coordinate must have zero coefficient
      have hFi0zero : ğ“• f {iâ‚€} = 0 := by
        symm
        calc 0 = (1 - 1)/2                  := by ring
             _ = (f 0 - f (flipAt iâ‚€ 0))/2 := by rw [hf1, hiâ‚€]
             _ = ((âˆ‘ i, ğ“• f {i} - âˆ‘ iâˆˆuniv.erase iâ‚€, ğ“• f {i}) + ğ“• f {iâ‚€})/2 :=
              by rw [hf0eq, hf0feq]; ring
             _ = ğ“• f {iâ‚€} :=
              by rw [â†sum_erase_add (a:=iâ‚€) (h:=mem_univ iâ‚€)]; ring
      -- apply inductive hypothesis to the fiber with iâ‚€ coordinate fixed
      let g : BooleanFunc (n+1) := Î» x => f (Fin.insertNth iâ‚€ 0 x)
      have hgeq: g = âˆ‘ i, ğ“• f {iâ‚€.succAbove i}â€¢Ï‡ {i} := by
        ext x
        simp
        unfold g
        nth_rewrite 1 [hf]
        rw [â†sum_erase_add (a:=iâ‚€), hFi0zero, zero_mul, add_zero]
        symm
        apply sum_of_injOn (e:=Î» i=>iâ‚€.succAbove i)
        Â· intro i _ j _
          dsimp
          exact succAbove_right_inj.mp
        Â· unfold Set.MapsTo
          intro i _
          simp
          apply succAbove_ne iâ‚€
        Â· intro i hi0 hi1
          simp at hi1
          simp at hi0
          contradiction
        Â· intro i _
          simp
        Â· exact mem_univ iâ‚€
      have : âˆ€ i, ğ“• g {i} = ğ“• f {iâ‚€.succAbove i} := by
        intro i
        calc _ = âŸ¨Ï‡ {i}, gâŸ©       := by rfl
             _ = âŸ¨Ï‡ {i}, âˆ‘ i, ğ“• f {iâ‚€.succAbove i}â€¢Ï‡ {i}âŸ© := by rw [hgeq]
             _ = âˆ‘ i', ğ“• f {iâ‚€.succAbove i'}*âŸ¨Ï‡ {i}, Ï‡ {i'}âŸ© := by
                  rw [inner_sum]; conv => enter[1,2,i']; rw [inner_smul_right]
             _ = ğ“• f {iâ‚€.succAbove i}                     := by
                  conv => enter [1,2,i']; rw [walsh_inner_eq]
                  simp
      have hgeq': âˆ€ x, g x = âˆ‘ i, ğ“• g {i}*(-1)^(x i).val := by
        intro x; nth_rewrite 1 [hgeq]; rw [sum_apply]; apply sum_congr (by rfl); intro i _; simp [this]
      have : g 0 = 1 := by unfold g; simp; exact hf1
      have : BooleanValued g := BooleanValued.mk
        (by intro x; exact hbv.one_or_neg_one (Fin.insertNth iâ‚€ 0 x))
      have := hi (f:=g) (Nat.succ_pos n) (by assumption) (by assumption)
      obtain âŸ¨S,hS1,hS2âŸ© := this
      obtain âŸ¨c,hcâŸ© := hS2
      simp at hS1
      obtain âŸ¨iâ‚,hiâ‚âŸ© := card_eq_one.mp hS1
      use {Fin.succAbove iâ‚€ iâ‚}
      constructor
      Â· simp
      Â· use c
        funext x
        simp
        -- f is independent of iâ‚€-coordinate
        have hfxi: f x = f (update x iâ‚€ 0) := by
          calc f x = âˆ‘ i, ğ“• f {i}*(-1)^(x i).val :=
                  by rw [hf]
                _  = âˆ‘ iâˆˆuniv.erase iâ‚€, ğ“• f {i}*(-1)^(x i).val + ğ“• f {iâ‚€}*(-1)^(x iâ‚€).val :=
                  by rw [sum_erase_add (h:=mem_univ iâ‚€)]
                _  = âˆ‘ iâˆˆuniv.erase iâ‚€, ğ“• f {i}*(-1)^(x i).val +   0*(-1)^(update x iâ‚€ 0 iâ‚€).val :=
                  by rw [hFi0zero]; simp
                _  = âˆ‘ iâˆˆuniv.erase iâ‚€, ğ“• f {i}*(-1)^(update x iâ‚€ 0 i).val +   0*(-1)^(update x iâ‚€ 0 iâ‚€).val :=
                  by {congr 1; apply sum_congr (by rfl);
                        intro i hi; apply ne_of_mem_erase at hi; rw [update_noteq hi]}
                _ = _ :=
                  by rw [â†hFi0zero, sum_erase_add (h:=mem_univ iâ‚€), â†hf]
        calc _ = f (Fin.insertNth iâ‚€ 0 (Fin.removeNth iâ‚€ x))  := by rw [hfxi, Fin.insertNth_removeNth]
              _ = g (Fin.removeNth iâ‚€ x)                      := by rfl
              _ = (câ€¢Ï‡ {iâ‚}) (Fin.removeNth iâ‚€ x)             := by rw [hc, hiâ‚]
              _ = _                                           := by simp; left; rfl

/-- A Boolean valued function with degree one Fourier weight equal to one
must be `Â±1` times a degree one character. -/
lemma eq_character_of_fourier_weight_one_eq_one' (hn:n>0) (hf:fourierWeight 1 f=1):
    âˆƒ Sâˆˆ{S|S.card=1}, âˆƒ c:â„, f = câ€¢Ï‡ S := by
  have hf' : âˆ€ x, f x = âˆ‘ i, ğ“• f {i}*(-1)^(x i).val := by
    apply eq_sum_degree_one_of_fourier_weight_one
    rw [hf, norm_sq_eq_one]
  exact eq_character_of_eq_sum_degree_one hn hf'

/-- A Boolean valued function with degree one Fourier weight equal to one
must be `Â±1` times a degree one character.
This is [odonnell2014], Exercise 1.19(a). -/
lemma eq_character_of_fourier_weight_one_eq_one (hn:n>0) (hf:fourierWeight 1 f=1):
    âˆƒ i, âˆƒ c:â„, f = câ€¢Ï‡ {i} := by
  obtain âŸ¨S, hS, hS'âŸ© := eq_character_of_fourier_weight_one_eq_one' hn hf
  obtain âŸ¨i, hiâŸ© := card_eq_one.mp hS
  use i
  rwa [hi] at hS'

end DegreeOne

section Distance

/-- The Hamming distance of two Boolean-valued functions is equal to
the proportion of inputs where they are not equal. The definition does not
require `f g` be Boolean-valued, but it will only be used in this context. -/
abbrev distance (f g:BooleanFunc n): â„ :=
  ğ„ (Î» xâ†¦oneOn (f xâ‰ g x))

lemma oneOn_eq_of_one_or_neg_one {x y:â„} (hx:x=1 âˆ¨ x=-1) (hy:y=1 âˆ¨ y=-1):
    oneOn (x=y) = (1/2)*(1+x*y) := by
  obtain âŸ¨hx|hx, hy|hyâŸ© := And.intro hx hy <;> { rw [hx, hy]; norm_num }

lemma oneOn_ne_of_one_or_neg_one {x y:â„} (hx:x=1 âˆ¨ x=-1) (hy:y=1 âˆ¨ y=-1):
    oneOn (xâ‰ y) = (1/2)*(1-x*y) := by
  obtain âŸ¨hx|hx, hy|hyâŸ© := And.intro hx hy <;> { rw [hx, hy]; norm_num }

lemma distance_eq: distance f g = ğ„ (Î» xâ†¦(1/2)*(1-(f x)*(g x))) := by
  unfold distance
  simp_rw [oneOn_ne_of_one_or_neg_one (hbv.one_or_neg_one _) (hbvg.one_or_neg_one _)]

lemma inner_eq_distance: âŸ¨f,gâŸ© = 1-2*distance f g := by
  rw [distance_eq]
  unfold expectation
  dsimp
  rw [â†mul_sum, sum_sub_distrib]
  ring_nf
  simp
  rw [mul_comm, â†inv_pow, â†one_div]
  rfl

end Distance

section BLR
/-!
  We introduce the BLR "linearity" test [Blum, Luby, Rubinfeld][blr1990]
  following O'Donnell [odonnell2014], Sec. 1.6.
-/

/-- The BLR test accepts `f` on independently and uniformly chosen `x y` if `(f x)*(f y)=f (x+y)`.
The acceptance probability is the proportion of inputs `x y` on which the test accepts. -/
abbrev acceptanceProbabilityBLR (f:BooleanFunc n): â„ :=
  ğ„ $ Î» x â†¦ ğ„ $ Î» y â†¦ oneOn $ (f x)*(f y)=f (x+y)

lemma acceptanceProbabilityBLR_eq: acceptanceProbabilityBLR f =
    (ğ„ $ Î» x â†¦ ğ„ $ Î» y â†¦ (1/2)*(1+(f x)*(f y)*(f (x+y)))) := by
  have hl: âˆ€ x y, (f x)*(f y) = 1 âˆ¨ (f x)*(f y) = -1 := by
    intro x y
    obtain âŸ¨hx|hx,hy|hyâŸ© := And.intro (hbv.one_or_neg_one x) (hbv.one_or_neg_one y)
      <;> { rw [hx, hy]; simp }
  unfold acceptanceProbabilityBLR
  simp_rw [oneOn_eq_of_one_or_neg_one (hl _ _) (hbv.one_or_neg_one _)]

omit hbv in
-- A "trivial" step in the proof of `almost_character`
-- would be nice if this could be done with very few tactics
private lemma _aux_lemma: (ğ„ $ Î» x â†¦ ğ„ $ Î» y â†¦ (1/2)*(1+(f x)*(f y)*(f (x+y))))
      = (1/2)*(1+(ğ„ $ Î» x â†¦ (f x)*(ğ„ $ Î» y â†¦ (f y)*(f (x+y))))) := by
    unfold expectation
    dsimp
    conv => enter [1,2,2,x]; rw [â†mul_sum, sum_add_distrib, mul_add, mul_add]
            simp; arg 2; rw [â†mul_assoc, mul_comm _ 2â»Â¹, mul_assoc]
            enter [2,2,2,y]; rw [mul_assoc]
    conv => enter [1,2,2,x,2]; rw [â†mul_sum]
            rw [â†mul_assoc, â†mul_assoc, mul_comm _ (f x), â†mul_assoc, mul_comm (f x) _]
            rw [mul_assoc, mul_assoc]
    rw [sum_add_distrib, Finset.sum_const]
    simp
    rw [â†mul_sum, â†mul_add, â†mul_assoc, mul_comm _ 2â»Â¹, mul_assoc]
    rw [mul_add, inv_mul_cancelâ‚€ (by simp)]

/-- The BLR test can detect that a Boolean valued function is close to being a character.
See [odonnell2014], Theorem 1.30. -/
theorem almost_character {Îµ:â„} (h:acceptanceProbabilityBLR fâ‰¥1-Îµ):
    âˆƒ S, distance f (Ï‡ S) â‰¤ Îµ := by
  have : 1-Îµ â‰¤ (1/2)*(1+âˆ‘ S, (ğ“• f S)*(ğ“• f S)^2) := by
    calc
      _ â‰¤ acceptanceProbabilityBLR f                                := h
      _ = (ğ„ $ Î» x â†¦ ğ„ $ Î» y â†¦ (1/2)*(1+(f x)*(f y)*(f (x+y))))   := acceptanceProbabilityBLR_eq
      _ = (1/2)*(1+(ğ„ $ Î» x â†¦ (f x)*(ğ„ $ Î» y â†¦ (f y)*(f (x+y))))) := _aux_lemma
      _ = (1/2)*(1+(ğ„ $ Î» x â†¦ (f x)*(fâ‹†f) x))                      := by rfl
      _ = (1/2)*(1+ğ„ (f*(fâ‹†f)))                                     := by rw [expectation_mul_apply]
      _ = (1/2)*(1+âˆ‘ S, (ğ“• f S)*(ğ“• (fâ‹†f) S))                        :=
        by rw [â†inner_eq_expectation, inner_eq_sum_fourier]
      _ = _ := by rw [fourier_convolution]; simp_rw [Pi.mul_apply, pow_two]
  have: âˆƒ Sâ‚€, âˆ€ S, ğ“• f S â‰¤ ğ“• f Sâ‚€ := Finite.exists_max (ğ“• f Â·)
  obtain âŸ¨Sâ‚€, hSâ‚€âŸ© := this
  have : 1-2*Îµ â‰¤ 1-2*distance f (Ï‡ Sâ‚€) := by
    calc
      _ â‰¤ âˆ‘ S, (ğ“• f S)*(ğ“• f S)^2  := by linarith
      _ â‰¤ âˆ‘ S, (ğ“• f Sâ‚€)*(ğ“• f S)^2 := by gcongr; exact hSâ‚€ _
      _ â‰¤ (ğ“• f Sâ‚€)*âˆ‘ S, |ğ“• f S|^2 := by simp_rw [sq_abs]; rw [mul_sum]
      _ = ğ“• f Sâ‚€                  := by rw [fourier_eq_one, mul_one]
      _ = âŸ¨f, Ï‡ Sâ‚€âŸ©                := by rw [fourier_eq_inner, inner_comm]
      _ = _                        := inner_eq_distance
  use Sâ‚€
  linarith


end BLR

end BV

end BooleanFun

--#lint
